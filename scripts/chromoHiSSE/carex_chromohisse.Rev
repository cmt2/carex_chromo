loadPlugin("TensorPhylo")

k    = 72
tree = readTrees("data/Carex_withoutSiderosticta.tree")[1]
age  = tree.rootAge()
taxa = tree.taxa()
seed(1)

# specify type of hidden model
bichromy = FALSE 
hidden_clado = TRUE

data = readCharacterDataDelimited("data/carex_chromosome_counts_expanded_withoutSiderosticta.tsv", 
                                  stateLabels=2*k +2, 
                                  type="NaturalNumbers", 
                                  delimiter="\t", 
                                  headers=FALSE)

mvi = 0

# rate matrix for chromosomes
gamma ~ dnExponential(2)
moves[++mvi] = mvScale(gamma)
gamma.setValue(6)

delta ~ dnExponential(2)
moves[++mvi] = mvScale(delta)
delta.setValue(6)

rho <- abs(0.0)

Q_chromo := fnChromosomes(maxChromosomes = k, 
                          gamma = gamma, 
                          delta = delta, 
                          rho = rho)

# rate matrix for hidden characters 
alpha ~ dnExponential(10)
moves[++mvi] = mvScale(alpha)

beta ~ dnExponential(10)
moves[++mvi] = mvScale(beta)

Q_hidden := fnFreeBinary( [alpha, beta], rescaled=FALSE )

# combined rate matrix
# do this by hand instead of covarion - is this function glitchy?
Q_combined := fnCovarionRateMatrix( [Q_chromo, Q_chromo], Q_hidden, [1,1], rescaled=FALSE )

# cladogenetic stuff

#empirical prior def
taxa <- tree.taxa()
speciation_mean <- ln( taxa.size() ) / tree.rootAge()
speciation_pr <- 1 / speciation_mean

# Each cladogenetic event type is assigned its own speciation rate.
total_speciation ~ dnExponential(speciation_pr)
moves[++mvi] = mvScale(total_speciation, lambda=0.2)
total_speciation.setValue(1.2)

relative_clado ~ dnDirichlet([1,1,1])
moves[++mvi] = mvBetaSimplex(relative_clado, alpha=10)
moves[++mvi] = mvElementSwapSimplex(relative_clado)
relative_clado.setValue(simplex([1,5,5]))

clado_rates := total_speciation * abs(relative_clado)

clado_no_change := clado_rates[1]
clado_fission   := clado_rates[2]
clado_fusion    := clado_rates[3]

# clado_no_change ~ dnExponential(speciation_pr)
# clado_fission ~ dnExponential(speciation_pr)
# clado_fusion ~ dnExponential(speciation_pr)
clado_polyploid <- 0.0
clado_demipoly <- 0.0

# this allows two 'modes' of chromosse across the tree 
if (bichromy == TRUE) {
	# create separate rates for hidden state B
	total_speciation_h ~ dnExponential(speciation_pr)
    moves[++mvi] = mvScale(total_speciation_h, lambda=0.2)
    total_speciation_h.setValue(1.2)
    
    relative_clado_h ~ dnDirichlet([1,1,1])
    moves[++mvi] = mvBetaSimplex(relative_clado_h, alpha=10)
    moves[++mvi] = mvElementSwapSimplex(relative_clado_h)
    relative_clado_h.setValue(simplex([1,5,5]))
    
    clado_rates_h := total_speciation_h * abs(relative_clado_h)
    
    clado_no_change_h := clado_rates_h[1]
    clado_fission_h   := clado_rates_h[2]
    clado_fusion_h    := clado_rates_h[3]
    
    # clado_no_change ~ dnExponential(speciation_pr)
    # clado_fission ~ dnExponential(speciation_pr)
    # clado_fusion ~ dnExponential(speciation_pr)
    clado_polyploid_h <- 0.0
    clado_demipoly_h <- 0.0
} 


 # this allows for a clado transition between the hidden states 
 if (hidden_clado == TRUE){
    # eg between 13a -> 13a + 13b
    chi_a ~ dnExponential(10)
    moves[++mvi] = mvScale(chi_a)
    # eg between 13b -> 13a + 13b
    chi_b ~ dnExponential(10)
    moves[++mvi] = mvScale(chi_b)
 } 

# write a very large loop thing that will build a matrix to account for transitions (add element for clado change between hidden using the chi rates)

ne = 0
clado_events[++ne] <- [1,1,1]
clado_probs[ne]    := Probability(1.0)

# cladogenetic transitions for the hidden state w/ 0 chromosomes
clado_events[++ne] <- [1 + k + 1,1 + k + 1,1 + k + 1]
clado_probs[ne]    := Probability(1.0)

# also specify the speciation rates
lambda[1] := 0
lambda[k+2] := 0

# right now no demipolyploidy
for(i in 1:k) { # this index is the number of chromosomes
	
	idx = i + 1 # because tensorphylo will drop the indexing by one
	idx_hidden = idx+k+1	
	if (i == 1) {

		# compute the total rate
		lambda[idx] := abs(clado_no_change + clado_fission + clado_polyploid + chi_a)
		
		# no change
		clado_events[++ne] <- [idx, idx, idx]
		clado_probs[ne]    := Probability(clado_no_change / lambda[idx])

		# increase by one
		clado_events[++ne] <- [idx, idx + 1, idx]
		clado_probs[ne]    := Probability(0.5 * (clado_fission + clado_polyploid) / lambda[idx])
		clado_events[++ne] <- [idx, idx, idx + 1]
		clado_probs[ne]    := Probability(0.5 * (clado_fission + clado_polyploid) / lambda[idx])

		# transition between hidden states
        clado_events[++ne] <- [idx, idx, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * chi_a / lambda[idx])
        clado_events[++ne] <- [idx, idx_hidden, idx]
        clado_probs[ne]    := Probability(0.5 * chi_a / lambda[idx])

        # HIDDEN
        # compute the total rate 
		lambda[idx_hidden] := abs(clado_no_change + clado_fission + clado_polyploid + chi_b)

        # no change
        clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden]
        clado_probs[ne]    := Probability(clado_no_change / lambda[idx_hidden])
        
        # increase by one
        clado_events[++ne] <- [idx_hidden, idx_hidden + 1, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * (clado_fission + clado_polyploid) / lambda[idx_hidden])
        clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden + 1]
        clado_probs[ne]    := Probability(0.5 * (clado_fission + clado_polyploid) / lambda[idx_hidden])

        # transition between hidden states
        clado_events[++ne] <- [idx_hidden, idx, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * chi_b / lambda[idx_hidden])
        clado_events[++ne] <- [idx_hidden, idx_hidden, idx]
        clado_probs[ne]    := Probability(0.5 * chi_b / lambda[idx_hidden])
		
	} else if ( i + i <= k ) { # polyploidization allowed
		
		# compute the total rate
		lambda[idx] := abs(clado_no_change + clado_fission + clado_fusion + clado_polyploid + chi_a)
		
		# no change
		clado_events[++ne] <- [idx, idx, idx]
		clado_probs[ne]    := Probability(clado_no_change / lambda[idx])
		
		# increase by one
		clado_events[++ne] <- [idx, idx + 1, idx]
		clado_probs[ne]    := Probability(0.5 * clado_fission / lambda[idx])
		clado_events[++ne] <- [idx, idx, idx + 1]
		clado_probs[ne]    := Probability(0.5 * clado_fission / lambda[idx])
		
		# decrease by one
		clado_events[++ne] <- [idx, idx - 1, idx]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx])
		clado_events[++ne] <- [idx, idx, idx - 1]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx])
		
		# polyploidization
		clado_events[++ne] <- [idx, i + i + 1, idx]
		clado_probs[ne]    := Probability(0.5 * clado_polyploid / lambda[idx])
		clado_events[++ne] <- [idx, idx, i + i + 1]
		clado_probs[ne]    := Probability(0.5 * clado_polyploid / lambda[idx])

		# transition between hidden states
        clado_events[++ne] <- [idx, idx, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * chi_a / lambda[idx])
        clado_events[++ne] <- [idx, idx_hidden, idx]
        clado_probs[ne]    := Probability(0.5 * chi_a / lambda[idx])

        # HIDDEN
		# compute the total rate
		lambda[idx_hidden] := abs(clado_no_change + clado_fission + clado_fusion + clado_polyploid + chi_b)
		
		# no change
		clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden]
		clado_probs[ne]    := Probability(clado_no_change / lambda[idx_hidden])
		
		# increase by one
		clado_events[++ne] <- [idx_hidden, idx_hidden + 1, idx_hidden]
		clado_probs[ne]    := Probability(0.5 * clado_fission / lambda[idx_hidden])
		clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden + 1]
		clado_probs[ne]    := Probability(0.5 * clado_fission / lambda[idx_hidden])
		
		# decrease by one
		clado_events[++ne] <- [idx_hidden, idx_hidden - 1, idx_hidden]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx_hidden])
		clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden - 1]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx_hidden])
		
		# polyploidization
		clado_events[++ne] <- [idx_hidden, i + i + 1, idx_hidden]
		clado_probs[ne]    := Probability(0.5 * clado_polyploid / lambda[idx_hidden])
		clado_events[++ne] <- [idx_hidden, idx_hidden, i + i + 1]
		clado_probs[ne]    := Probability(0.5 * clado_polyploid / lambda[idx_hidden])

		# transition between hidden states
        clado_events[++ne] <- [idx_hidden, idx, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * chi_b / lambda[idx_hidden])
        clado_events[++ne] <- [idx_hidden, idx_hidden, idx]
        clado_probs[ne]    := Probability(0.5 * chi_b / lambda[idx_hidden])

	} else if ( i < k ) { # fissuion but no polyploidy
		
		# compute the total rate
		lambda[idx] := abs(clado_no_change + clado_fission + clado_fusion + chi_a)

		# no change
		clado_events[++ne] <- [idx, idx, idx]
		clado_probs[ne]    := Probability(clado_no_change / lambda[idx])

		# increase by one
		clado_events[++ne] <- [idx, idx + 1, idx]
		clado_probs[ne]    := Probability(0.5 * clado_fission / lambda[idx])
		clado_events[++ne] <- [idx, idx, idx + 1]
		clado_probs[ne]    := Probability(0.5 * clado_fission / lambda[idx])
		
		# decrease by one
		clado_events[++ne] <- [idx, idx - 1, idx]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx])
		clado_events[++ne] <- [idx, idx, idx - 1]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx])		

		# transition between hidden states
        clado_events[++ne] <- [idx, idx, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * chi_a / lambda[idx])
        clado_events[++ne] <- [idx, idx_hidden, idx]
        clado_probs[ne]    := Probability(0.5 * chi_a / lambda[idx])

		# HIDDEN
		# compute the total rate
		lambda[idx_hidden] := abs(clado_no_change + clado_fission + clado_fusion + chi_b)

		# no change
		clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden]
		clado_probs[ne]    := Probability(clado_no_change / lambda[idx_hidden])

		# increase by one
		clado_events[++ne] <- [idx_hidden, idx_hidden + 1, idx_hidden]
		clado_probs[ne]    := Probability(0.5 * clado_fission / lambda[idx_hidden])
		clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden + 1]
		clado_probs[ne]    := Probability(0.5 * clado_fission / lambda[idx_hidden])
		
		# decrease by one
		clado_events[++ne] <- [idx_hidden, idx_hidden - 1, idx_hidden]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx_hidden])
		clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden - 1]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx_hidden])	

		# transition between hidden states
        clado_events[++ne] <- [idx_hidden, idx, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * chi_b / lambda[idx_hidden])
        clado_events[++ne] <- [idx_hidden, idx_hidden, idx]
        clado_probs[ne]    := Probability(0.5 * chi_b / lambda[idx_hidden])

	} else { # maximum state -- no fission or polyploidy

		# compute the total rate
		lambda[idx] := abs(clado_no_change + clado_fusion + chi_a)

		# no change
		clado_events[++ne] <- [idx, idx, idx]
		clado_probs[ne]    := Probability(clado_no_change / lambda[idx])
		
		# decrease by one
		clado_events[++ne] <- [idx, idx - 1, idx]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx])
		clado_events[++ne] <- [idx, idx, idx - 1]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx])	

		# transition between hidden states
        clado_events[++ne] <- [idx, idx, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * chi_a / lambda[idx])
        clado_events[++ne] <- [idx, idx_hidden, idx]
        clado_probs[ne]    := Probability(0.5 * chi_a / lambda[idx])

		# HIDDEN	
		# compute the total rate
		lambda[idx_hidden] := abs(clado_no_change + clado_fusion + chi_b)

		# no change
		clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden]
		clado_probs[ne]    := Probability(clado_no_change / lambda[idx_hidden])
		
		# decrease by one
		clado_events[++ne] <- [idx_hidden, idx_hidden - 1, idx_hidden]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx_hidden])
		clado_events[++ne] <- [idx_hidden, idx_hidden, idx_hidden - 1]
		clado_probs[ne]    := Probability(0.5 * clado_fusion / lambda[idx])		

		# transition between hidden states
        clado_events[++ne] <- [idx_hidden, idx, idx_hidden]
        clado_probs[ne]    := Probability(0.5 * chi_b / lambda[idx_hidden])
        clado_events[++ne] <- [idx_hidden, idx_hidden, idx]
        clado_probs[ne]    := Probability(0.5 * chi_b / lambda[idx_hidden])
	}

}


# map the speciation rates to chromosome cladogenetic events
omega := fnCladogeneticProbabilityMatrix(clado_events, clado_probs, 2*k + 2)

# diversification parameters
# total_speciation := clado_no_change + clado_fission + clado_fusion + clado_polyploid + clado_demipoly
turnover ~ dnExponential(speciation_pr)
turnover.setValue(0.85)
moves[++mvi] = mvScale(turnover, lambda=0.5)
for (i in 1:(2*k + 2)) {
    mu[i] := turnover
}

moves[++mvi] = mvUpDownScale(weight=1, lambda=0.5)
# moves[mvi].addVariable(clado_no_change, up=true)
# moves[mvi].addVariable(clado_fission, up=true)
moves[mvi].addVariable(total_speciation, up=true)
moves[mvi].addVariable(turnover, up=true)


frac <- 0.4
root_freq_abs <-  rep(1, 2 * k + 2)
root_freq_abs[1] <- 0
root_freq_abs[1 + k + 1] <- 0
root_frequencies <- simplex(root_freq_abs)
condition        <- "time"

# the distribution
x ~ dnGLHBDSP(rootAge   = age,
              lambda    = lambda,
              mu        = mu,
              eta       = Q_combined,
              omega     = omega,
              pi        = root_frequencies,
              rho       = frac,
              condition = condition,
              taxa      = taxa,
              nStates   = 2 * (k + 1),
              nProc     = 16,
		      zeroIndex = FALSE)
x.clamp(tree)
x.clampCharData(data)
x.lnProbability()

# model
my_model = model(x)

# monitors
monitors[1] = mnScreen(printgen=1)
monitors[2] = mnModel(printgen=1, file="output/chromohisse_run1.log")
#monitors[3] = mnJointConditionalAncestralState(filename="output/ChromoSSE_anc_states.log", printgen=1, tree=tree, cdbdp=x, withStartStates=true, type="NaturalNumbers")

# analysis
my_mcmc = mcmc(my_model, monitors, moves)
#my_mcmc.burnin(500, tuningInterval=50)
#my_mcmc.initializeFromCheckpoint("output/chromohisse_run1.state") # to restart from checkpoint
#my_mcmc.run(generations=500, tuningInterval=10)
my_mcmc.run(generations=500)
my_mcmc.run(generations=25000,
			checkpointInterval=100, 
			checkpointFile="output/chromohisse_run1.state")
#my_mcmc.operatorSummary()



q()
